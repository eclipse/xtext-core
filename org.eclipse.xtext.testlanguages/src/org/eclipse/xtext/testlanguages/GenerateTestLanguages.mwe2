module org.eclipse.xtext.testlanguages.GenerateTestLanguages

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {

	component = XtextGenerator {
		configuration = {
			project = model.project.StandardProjectConfig {
				baseName = "org.eclipse.xtext.testlanguages"
				rootPath = rootPath
				
				runtimeTest = {
					enabled = true
					root = "${rootPath}/org.eclipse.xtext.testlanguages"	
				}

				eclipsePlugin = {
					enabled = true
					root = "${rootPath}/../xtext-eclipse/org.eclipse.xtext.testlanguages.ui"	
				}
				eclipsePluginTest = {
					enabled = true
					root = "${rootPath}/../xtext-eclipse/org.eclipse.xtext.testlanguages.ui"	
				}
				ideaPlugin = {
					enabled = false
					root = "${rootPath}/../xtext-intellij/org.eclipse.xtext.testlanguages.idea"	
				}
				
				createEclipseMetaData = true
			}
			code = {
				encoding = 'ISO-8859-1'
				lineDelimiter = "\n"
				preferXtendStubs = false
			}
		}

		language = @backtracking.BeeLangTestLanguage {}
		language = @backtracking.SimpleBeeLangTestLanguage {}
		language = @backtracking.ExBeeLangTestLanguage {}
		language = @noJdt.NoJdtTestLanguage {}
		language = @xtextgrammar.XtextGrammarTestLanguage {}
		language = @fileAware.FileAwareTestLanguage {}
	}
}
