/*******************************************************************************
 * Copyright (c) 2008 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 *******************************************************************************/
grammar org.eclipse.xtext.testlanguages.SimpleExpressionsTestLanguage with org.eclipse.xtext.common.Terminals

generate simpleExpressions "http://www.eclipse.org/xtext/test/simpleExpressions"

Sequence: Addition ( {Sequence.expressions+=current} expressions+=Addition)*;

Addition returns Expression: 
	Multiplication ( {Op.values+=current} operator=('+'|'-') values+=Multiplication)*;

Multiplication returns Expression: 
	Term ( {Op.values+=current} operator=('*'|'/') values+=Term)*;
	
Term returns Expression:
	Atom | Parens;
	
Atom: 
	name=ID;

Parens returns Expression: 
	'(' Addition ')';
	



