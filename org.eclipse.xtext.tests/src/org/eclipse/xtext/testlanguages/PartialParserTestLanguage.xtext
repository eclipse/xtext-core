/*******************************************************************************
 * Copyright (c) 2008, 2017 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 *******************************************************************************/
grammar org.eclipse.xtext.testlanguages.PartialParserTestLanguage with org.eclipse.xtext.common.Terminals

generate partialParserTestLanguage "http://example.xtext.org/PartialParserTestLanguage"

SomeContainer :
  'container' name=ID '{'
     ( nested+=Nested | content+=Content )*
  '}';

Nested:
	'nested' '{' 
     	(nested+=SomeContainer)+ 
    '}'; 

Content:
	Children | AbstractChildren;

Children:
	'children' '{'
     	children += Child (',' children += Child )*
    '}';
    
Child :
  '->' 'C' '(' value=Named ')';
  
AbstractChildren :
	/* SuppressWarnings[SpacesInKeyword] */
	'abstract children' '{'
   		( abstractChildren += AbstractChild )+
  	'}'; 
     	
AbstractChild :
  FirstConcrete | SecondConcrete;
  
FirstConcrete :
  '->' 'F' '(' value=Named referencedContainer=[SomeContainer]? ')';
  
SecondConcrete :
  '->' 'F' 'S' '(' value=Named referencedChildren+=[Child]? ')';
  
Named:
  name=ID;
  
