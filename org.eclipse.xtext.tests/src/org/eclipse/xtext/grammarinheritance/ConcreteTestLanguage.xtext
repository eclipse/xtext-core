/*******************************************************************************
 * Copyright (c) 2008, 2017 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
grammar org.eclipse.xtext.grammarinheritance.ConcreteTestLanguage with org.eclipse.xtext.grammarinheritance.AbstractTestLanguage

generate foo "http://holla"

import "http://www.eclipse.org/xtext/tests/grammarinheritance" as mm

RootRule:
	ConcreteParserRule | CallOverridenParserRule | CallExtendedParserRule | OverridableParserRule2;

// call InheritedParserRule
ConcreteParserRule :
	'model' magicNumber=REAL ':' (elements+=InheritedParserRule)*;
	
// override OverridableParserRule and call it
@Override 
OverridableParserRule returns mm::AType:
	'overriddenelement' name=ID;
	
CallOverridenParserRule:
	call=AbstractCallOverridenParserRule;
	
// override parser rule and change return type
/* SuppressWarnings[SpacesInKeyword] */
@Override 
OverridableParserRule2 returns AType2 :
	'overridden other element' name=ID '-' age=INT;
	
// override and extend ExtendableParserRule and call it
@Override 
ExtendableParserRule returns mm::AType:
	Subrule1 | Subrule2 | Subrule3;
	
Subrule1:
	'subrule1' name=ID sub1=ID;

Subrule2:
	'subrule3' name=ID sub2=STRING;

Subrule3:
	'subrule3' name=ID sub1=INT;
	
CallExtendedParserRule:
	call=AbstractCallExtendedParserRule;
	
@Override 
terminal ID:
	super
;
