/*******************************************************************************
 * Copyright (c) 2008 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 *******************************************************************************/
grammar org.eclipse.xtext.parsetree.formatter.FormatterTestLanguage with org.eclipse.xtext.common.Terminals

generate formattertestlanguage "http://simple/formattertestlanguage"

Root:
	"test" (TestLinewrap | TestIndentation | TestLinewrapMinMax | WrappingDataTypeTest);
	
Line:
	(Decl | Assign | Meth | FqnObj | FqnRef | Enumeration | SuppressedHidden "post" | Space | Datatypes) ";";
	
Decl:	
	type+=ID name+=ID;
	
Assign:
	var=ID op=("="|"+=") "[" (val+=INT ("," val+=INT)*)? "]";
	
Meth:
	"void" name=ID "(" (param+=Param ("," param+=Param)*)? ")";
	
Param:
	name+=ID ":" type+=ID;
	
Space: "space" val=ID;
	
TestLinewrap:
	{TestLinewrap} "linewrap" items+=Line*;
	
TestLinewrapMinMax:
	{TestLinewrapMinMax} "wrapminmax" items+=Line*;
	
TestIndentation:
	{TestIndentation} "indentation" "{" items+=(Line | TestIndentation)* "}" semi?=";"?;
	
FqnObj:
	"fqn" name=FQN;
	
FQN:
	ID ("." ID)*; 

FqnRef:
	"fqnref" ref=[FqnObj|FQN];
	
Enumeration:
	"enum" val+=Enum1+ (',' val+=Enum1)*;
	
enum Enum1: 
	lit1 | lit2 | lit3;

SuppressedHidden hidden():
	{SuppressedHidden} "`" (vals+=SuppressedHiddenSub ('%' vals+=SuppressedHiddenSub)*)? "`";
	
SuppressedHiddenSub:
	SuppressedHiddenSubSub | SuppressedHiddenSubID;

SuppressedHiddenSubSub hidden(WS):
	'<' idval=ID '>';
	
SuppressedHiddenSubID:
	idval=ID;
	
Datatype1: FQN;
Datatype2: FQN;
Datatype3: FQN;

Datatypes: "datatypes" val1=Datatype1 "kw1" val2=Datatype2 val3=Datatype3 "kw3";


WrappingDataTypeTest:
	"wrappingdt" datatype=WrappingDataType "kw1";
	
WrappingDataType:
	ID+; 