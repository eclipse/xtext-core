/*******************************************************************************
 * Copyright (c) 2008 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 *******************************************************************************/
grammar org.eclipse.xtext.serializer.AssignmentFinderTestLanguage with org.eclipse.xtext.common.Terminals

generate assignmentFinderTest "http://www.eclipse.org/2009/tmf/xtext/assignmentfindertestlanguage"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	("#00" keywordVal=KeywordVal) | 
	("#01" terminalVal=TerminalVal) | 
	("#02" enumVal=EnumVal) |
	("#03" keywordBool=KeywordBool) | 
	("#04" terminalBool=TerminalBool) | 
	("#05" enumBool=EnumBool) |
	("#06" mixedBool=MixedBool) |
	("#07" mixedValue=MixedValue) |
	("#08" containmentRef=ContainmentRef) |  
	("#09" crossRef=CrossRef);
	
KeywordVal: kw="kw1" | kw="kw2";
	
TerminalVal: term=Terminal1 | term=Terminal2 | term="%foo";
/* SuppressWarnings[InvalidTerminalruleName] */
terminal Terminal1: '%' ('A'..'Z')*;
/* SuppressWarnings[InvalidTerminalruleName] */
terminal Terminal2: '%' ('a'..'z')*;

EnumVal: en=Enum1 | en=Enum2;
enum Enum1 returns TestEnum: lit1; 
enum Enum2 returns TestEnum: lit2;
enum Enum3 returns TestEnum: lit3;

KeywordBool: kw?="kw1" | kw?="kw2";

TerminalBool: term?=Terminal1 | term?=Terminal2 | term?="%foo";

EnumBool: en?=Enum1 | en?=Enum2;

MixedBool: val?="kw1" | val=Boolean;
Boolean returns ecore::EBoolean: "true" | "false";

MixedValue: val=Enum1 | val=DatEnum;
DatEnum returns TestEnum: "lit3"; 

ContainmentRef: ctx=ContainmentRef1 | ctx=ContainmentRef2;
ContainmentRef1 returns ContainmentRefN: "kw1" val1=ID;
ContainmentRef2 returns ContainmentRefN: "kw2" val2=ID;

CrossRef: name=(Terminal1|Terminal2) (crossRef=[CrossRef|Terminal1] | crossRef=[CrossRef|Terminal2]);