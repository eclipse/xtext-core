/*******************************************************************************
 * Copyright (c) 2010 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
module org.eclipse.xtext.parsetree.resource.EObjectAtOffsetTestLanguage

import org.eclipse.emf.mwe2.runtime.workflow.Workflow
import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.*
import org.eclipse.xtext.xtext.generator.*

var projectName = "org.eclipse.xtext.tests"
var runtimeProject = "../${projectName}"
var lineDelimiter = '\n'

Workflow {
	bean = StandaloneSetup {
		scanClassPath = true
		platformUri = "${runtimeProject}/.."
		registerGenModelFile = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
	}
	
	component = DirectoryCleaner {
		directory = "${runtimeProject}/src-gen/org/eclipse/xtext/resource"
	}
	
	component = XtextGenerator {
		cleaner = {
			enabled = false
		}
		configuration = {
			project = model.project.StandardProjectConfig {
				baseName = projectName
				rootPath = ".."
			}
			code = {
				encoding = 'ISO-8859-1'
				lineDelimiter = lineDelimiter
				preferXtendStubs = false
			}
		}
		
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/resource/EObjectAtOffsetTestLanguage.xtext"
			fragment = @TestLanguagesFragments {}
		}
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/resource/LocationProviderTestLanguage.xtext"
			fragment = @TestLanguagesFragments {}
		}
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/resource/LiveContainerTestLanguage.xtext"
			fragment = @TestLanguagesFragments {}
		}
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/resource/LiveContainerBuilderIntegerationTestLanguage.xtext"
			fragment = @TestLanguagesFragments {}
			fragment = xtext.generator.builder.BuilderIntegrationFragment2 {}
		}
		language = {
			grammarUri = "classpath:/org/eclipse/xtext/resource/Bug385636TestLanguage.xtext"
			fragment = @TestLanguagesFragments {}
		}
	}
}
