/*******************************************************************************
 * Copyright (c) 2008 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 *******************************************************************************/
grammar org.eclipse.xtext.parser.terminalrules.TerminalRulesTestLanguage

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate terminalRulesTestLanguage "http://www.eclipse.org/2008/tmf/xtext/TerminalRulesTest"
 
Model:
  idValue=ID | 
  intValue=INT | 
  stringValue=STRING |
  richStringValue=RICH_STRING | 
  mlCommentValue=ML_COMMENT | 
  slCommentValue=SL_COMMENT | 
  wsValue=WS  |
  anyValue=ANY_OTHER
;
  
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal INT 		: ('0'..'9')+;
terminal STRING	: 
			'"' ( ESCAPED_CHAR | !('\\'|'"') )* '"' |
			"'" ( ESCAPED_CHAR | !('\\'|"'") )* "'"
		;
		
terminal RICH_STRING : "'''" IN_RICH_STRING* ("'''"| ("'" "'"?)? EOF);

terminal fragment IN_RICH_STRING : 
	  "''" !('«'|"'") 
	| "'" !('«'|"'")
	| !('«'|"'");


terminal fragment ESCAPED_CHAR: '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\');		

terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;