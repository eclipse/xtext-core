plugins.withType(JavaBasePlugin) {
	sourceCompatibility = '1.8'
}

tasks.withType(JavaCompile) {
	options.encoding = 'ISO-8859-1'
}

tasks.withType(Javadoc) {
	options.encoding = 'ISO-8859-1'
	options.tags = [ 'noreference', 'noextend', 'noimplement', 'noinstantiate', 'nooverride', 'model', 'generated', 'ordered' ] 
	options.addStringOption('Xdoclint:none', '-quiet')
}

configurations {
    optional {
    	description 'Dependencies required at build time, but not exported into meta data'
        extendsFrom compile
    }
    
	/*
	 * Put any unwanted transitive dependencies here, they will be excluded from all projects
	 */
    all {
    	exclude group: 'org.apache.felix', module: 'org.osgi.foundation'
		exclude group: 'org.antlr', module: 'stringtemplate'
		exclude module: 'cglib'
	}
}

sourceSets.main.compileClasspath += configurations.optional
sourceSets.test.compileClasspath += configurations.optional
sourceSets.test.runtimeClasspath += configurations.optional
eclipse.classpath.plusConfigurations += [configurations.optional]
javadoc.classpath += configurations.optional
