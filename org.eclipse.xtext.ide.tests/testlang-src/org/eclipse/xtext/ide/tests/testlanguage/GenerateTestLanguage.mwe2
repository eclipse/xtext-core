/*
 * Copyright (c) 2016 TypeFox GmbH (http://www.typefox.io) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 */
module org.eclipse.xtext.ide.tests.testlanguage.GenerateTestLanguage

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var baseName = "org.eclipse.xtext.ide.tests"

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = baseName
				rootPath = rootPath
				runtime = {
				    src = "${rootPath}/${baseName}/testlang-src"
				    srcGen = "${rootPath}/${baseName}/testlang-src-gen"
				}
				runtimeTest = {
					enabled = true
					name = baseName
				    src = "${rootPath}/${baseName}/testlang-src"
				    srcGen = "${rootPath}/${baseName}/testlang-src-gen"
				}
				genericIde = {
					enabled = true
				    name = baseName
				    src = "${rootPath}/${baseName}/testlang-src"
				    srcGen = "${rootPath}/${baseName}/testlang-src-gen"
				}
				createEclipseMetaData = false
			}
			code = {
				encoding = "UTF-8"
				fileHeader = 
"/*
 * Copyright (c) 2016 TypeFox GmbH (http://www.typefox.io) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 */
"
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.xtext.ide.tests.testlanguage.TestLanguage"
			fileExtensions = "testlang"

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			junitSupport = {
				generateStub = false
			}
		}
		language = StandardLanguage {
			name = "org.eclipse.xtext.ide.tests.testlanguage.PartialContentAssistTestLanguage"
			fileExtensions = "partialcontentassisttestlang"
			serializer = {
				generateStub = false
			}
			validator = {
			// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			junitSupport = {
				generateStub = false
			}
			parserGenerator = {
				partialParsing = true
			}
		}
		language = StandardLanguage {
			referencedResource = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
			name = "org.eclipse.xtext.ide.tests.testlanguage.PartialSerializationTestLanguage"
			fileExtensions = "pstl"
			serializer = {
				generateStub = false
			}
			validator = {
			// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			junitSupport = {
				generateStub = false
			}
			parserGenerator = {
				partialParsing = true
			}
			formatter = {
				generateStub = true
			}
		}
	}
}
